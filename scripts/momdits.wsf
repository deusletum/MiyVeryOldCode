<?xml version="1.0" ?>
<!--                                                                        -->
<!-- XML Source File    Created with SAPIEN Technologies PrimalSCRIPT(TM)   -->
<!--                                                                        -->
<!-- NAME: <filename>                                       				-->
<!--                                                                        -->
<!-- AUTHOR: Microsoft Employee , Microsoft                                 -->
<!-- DATE  : 7/26/2002                                                      -->
<!--                                                                        -->
<!--COMMENT: <comment>                                                      -->
<!--                                                                        -->
<!--                                                                        -->
<?job error="true" debug="true" ?>
<package>
	<job id="mom test bvt">
		<script language="VBScript" src="\\acstools\ToolBox\DITSUtils\DITSTasks.vbs"/>
		<script language="VBScript">
<![CDATA[
' -------------------------------------------------------------------
' company: oration
'

'
' summary:
'	This script will create a MOM TEST Dits job based on the 
'	arguments the user inputs
'
' history:
'	06-Aug-02 deangj First Creation
'    
'-------------------------------------------------------------------

Option Explicit

Main()

' -------------------------------------------------------------------
' 
' summary:
'	Main secion of the script
' 
' remarks:
'	Will perform all DITS tasks as well as parse command line
'	arguments
'
' param: 
'	Command line string arguments
' 	<momserver> is the computer to install MOM on
' 	<momagent> is the couputer to install MOM agent on
' 	<languages> are USA, FR, JPN, and GER
' 	<test> is BVT or FUNC
' 	<notify list> list of users to be notified about status from DITS
' 		delimit the user with a comma
' 	<owner> user owns the DITS job, must be a user alias
' 	<jobname> the name of this DITS job eg. MOM BVTs, MOM Funcs W2K etc.
'
' returns:
'	returns void
'
' history:
'	06-Aug-02 deangj First Creation
'	    
' -------------------------------------------------------------------
Sub Main()

Dim objJobXML 				'XML Doc Dits object
Dim objJobNode 				'Dits Job object
Dim objMachineNode 			'Dits Machine object
Dim objTasklistNode 		'Dits Task list object
Dim objEnvironNode 			'Dits Environment Variable
Dim objTaskNode 			'Dits Task object
Dim strRunTest 				'MOM BVT command string
Dim strRunMOMFUNC 			'MOM Func command string
Dim strOfficeInst 			'Install string for Office
Dim strSQLcmd 				'Install string for SQL Server
Dim strMOMServer 			'MOM Server Name
Dim strMOMAgent 			'MOM Agent Name
Dim strLang 				'the langusge you need
Dim strTest 				'The test to run
Dim strDITSServer 			'DITS Server name
Dim strEmail 				'Email Notify list
Dim strOwner 				'Dits Job Owner
Dim strJobName 				'Dits Job Name
Dim objCommand				'Ref to comand object to set command options

If WScript.Arguments.count < 7 Then
	Usage()
	WScript.Quit(0)
End If

strMOMServer = WScript.Arguments(0)
strMOMAgent = WScript.Arguments(1)
strLang = WScript.Arguments(2)
strTest = WScript.Arguments(3)
strEmail = WScript.Arguments(4)
strOwner = WScript.Arguments(5)
strJobName = WScript.Arguments(6)

	'This is the command to install sql rtm enterprise
strSQLcmd = "\\acstools\drop\sqlinst.cmd rtm enterprise"

strRunTest = "\\astdrop\drop\mom10sp1\x86\momtest.bat b:latest l:" & strTest & " t:" & Chr(34) & _
	"all" & Chr(34) & " d:both a:" & Chr(34) & strMOMAgent & Chr(34) & " m:no"
strOfficeInst = "\\dean_srv\tools\offinst.cmd Access 2k /NOREBOOT"

	'Start the XML Job
Set objJobXML = MakeJobXML()

	'Create a new job
Set objJobNode = MakeNewJob(strJobName, strEmail, strOwner, "Server Management Test", 0, "", "", "")
objJobXML.appendChild objJobNode

	'Create a New machine
Set objMachineNode = MakeNewMachine(objJobNode, Null, strMOMServer, "", "", "", "")

	'Create a new task list
Set objTasklistNode = MakeNewTaskList(objMachineNode, "BVTs", "", "", "")

Set objEnvironNode = MakeNewEnvironment(objTasklistNode, "loc", strLang)

	'Install  DITS Client
Set objTaskNode = MakeStandardTask(objTasklistNode, "DITSInit", Null, "", "", "")

	'Create a test to install SQL Server 2000 Enterprise
Set objTaskNode = MakeNewTask(objTasklistNode, "Install SQL", False, False, "", "", "")
MakeNewCommand objTaskNode, strSQLcmd, -1, 0, "", "", ""

	'Create a test to install Access 2000
Set objTaskNode = MakeNewTask(objTasklistNode, "Access install", False, False, "", "", "")
Set objCommand = MakeNewCommand(objTaskNode, strOfficeInst, 0, 0, "", "", "")
	'Reboot after access install
SetCommandOptions objCommand, 0, 1, True, -0, 0, "", False, 0, ""

	'Run MOM tests
Set objTaskNode = MakeNewTask(objTasklistNode, "RUN MOM " & strTest, False, False, "", "", "")
MakeNewCommand objTaskNode, strRunTest, -1, 0, "", "", "" 

	'Termationate DITS
Set objTaskNode = MakeStandardTask(objTasklistNode, "DITSTerm" , Null, "", "", "")

	'Set the DITS server
strDITSServer = "ACDITS"

	'Submit the DITS Job
SubmitJob objJobXML, strDITSServer, False, False
End Sub

' -------------------------------------------------------------------
' 
' summary:
'	Displays usage information to the user.
' 
' remarks:
'	When function is called it prints all command arguments to the
'	console.
' 
'
' returns:
'	returns void
'
' history:
'	06-Aug-02 deangj First Creation
'	    
' -------------------------------------------------------------------
Function Usage()
	WSCript.Echo "Usage: momtestbvt.wsf <momserver> <momagent> <language> " & _
		"<test> <notifylist> <owner> <jobname>"
	WScript.Echo "<momserver> is the computer to install MOM on"
	WScript.Echo "<momagent> is the couputer to install MOM agent on"
	WScript.Echo "<languages> are USA, FR, JPN, and GER"
	WScript.Echo "<test> is BVT or FUNC"
	WScript.Echo "<notify list> list of users to be notified about status from DITS"
	WScript.Echo vbTab & "example: asttest,momuser"
	WScript.Echo vbTab & "delimit the user with a comma"
	WScript.Echo "<owner> user owns the DITS job, must be a user alias"
	WScript.Echo "<jobname> the name of this DITS job eg. MOM BVTs, MOM Funcs W2K etc."
	WScript.Echo vbTab & "If <jobname> is more than one word, pleae " & _
		"enclose in " & Chr(34) & Chr(34)
	WScript.Echo vbTab & "Example " & Chr(34) & "MOM Funcs W2K SP2" & Chr(34)
End Function
]]>
		</script>
	</job>
</package>
